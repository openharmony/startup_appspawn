# Copyright (c) 2026 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/startup/appspawn/appspawn.gni")
import("//build/test.gni")

if (!defined(ohos_lite)) {
  ohos_unittest("AppSpawn_CodeSign_Test") {
    module_out_path = "appspawn/appspawn"
    cflags = [ "-Dprivate=public" ]

    if (appspawn_unittest_coverage) {
      cflags += [ "--coverage" ]
      ldflags = [ "--coverage" ]
      cflags_cc = [ "--coverage" ]
    }

    deps = []
    defines = [
      "APPSPAWN_BASE_DIR=\"/data/appspawn_ut\"",
      "APPSPAWN_LABEL=\"APPSPAWN_UT\"",
      "APPSPAWN_TEST",
      "APPSPAWN_CLIENT",
      "APPSPAWN_DEBUG",
      "DEBUG_BEGETCTL_BOOT",
      "CODE_SIGNATURE_ENABLE",
    ]

    # Target code include dir
    include_dirs = [
      "${appspawn_path}/common",
      "${appspawn_path}/modules/common",
      "${appspawn_path}/modules/modulemgr",
      "${appspawn_path}/modules/module_engine/include",
      "${appspawn_path}/standard",
      "${appspawn_path}/modules/sysevent",
      "${appspawn_innerkits_path}/client",
      "${appspawn_innerkits_path}/permission",
      "${appspawn_path}/modules/sandbox",
    ]

    # Test specific include dir
    include_dirs += [ "${appspawn_path}/test/unittest/app_spawn_standard_test/app_spawn_codesign_test" ]

    # Target code sources
    sources = [
      "${appspawn_path}/standard/appspawn_service.c",
    ]

    # Dependent code sources
    sources += [
      "${appspawn_path}/modules/modulemgr/appspawn_modulemgr.c",
      "${appspawn_path}/util/src/appspawn_utils.c",
      "${appspawn_path}/util/src/appspawndf_utils.cpp",
      "${appspawn_path}/standard/appspawn_appmgr.c",
      "${appspawn_path}/standard/appspawn_msgmgr.c",
      "${appspawn_innerkits_path}/client/appspawn_client.c",
      "${appspawn_innerkits_path}/client/appspawn_msg.c",
      "${appspawn_innerkits_path}/permission/appspawn_mount_permission.c",
      "${appspawn_path}/modules/sandbox/appspawn_permission.c",
      "${appspawn_path}/modules/common/appspawn_common.c",
      "${appspawn_path}/common/appspawn_trace.cpp",
      "${appspawn_path}/modules/common/appspawn_adapter.cpp",
      "${appspawn_path}/modules/common/appspawn_silk.c",
    ]

    # Test specific sources
    sources += [
      "app_spawn_codesign_test.cpp",
      "codesign_test_helper.cpp",
    ]

    configs = [ "${appspawn_path}:appspawn_config" ]
    external_deps = [
      "access_token:libaccesstoken_sdk",
      "access_token:libtokenid_sdk",
      "access_token:libtokensetproc_shared",
      "cJSON:cjson",
      "code_signature:libcode_sign_attr_utils",
      "c_utils:utils",
      "config_policy:configpolicy_util",
      "googletest:gmock_main",
      "hilog:libhilog",
      "hitrace:hitrace_meter",
      "init:libbegetutil",
      "init:seccomp",
    ]

    if (appspawn_report_event) {
      defines += [ "REPORT_EVENT" ]
      external_deps += [ "hisysevent:libhisysevent" ]
    }

    if (build_selinux) {
      defines += [ "WITH_SELINUX" ]
      external_deps += [
        "selinux:libselinux",
        "selinux_adapter:libhap_restorecon",
      ]
    }
  }
}
