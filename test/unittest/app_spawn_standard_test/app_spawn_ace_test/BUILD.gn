# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/startup/appspawn/appspawn.gni")
import("//build/test.gni")

if (!defined(ohos_lite)) {
  ohos_unittest("AppSpawn_Ace_Test") {
    module_out_path = "appspawn/appspawn"
    cflags = [ "-Dprivate=public" ]

    if (appspawn_unittest_coverage) {
      cflags += [ "--coverage" ]
      ldflags = [ "--coverage" ]
      cflags_cc = [ "--coverage" ]
    }

    deps = []
    defines = [
      "APPSPAWN_BASE_DIR=\"/data/appspawn_ut\"",
      "APPSPAWN_LABEL=\"APPSPAWN_UT\"",
      "APPSPAWN_TEST",
      "APPSPAWN_CLIENT",
      "APPSPAWN_DEBUG",
    ]

    # Target code include dir
    include_dirs = [
      "${appspawn_path}",
      "${appspawn_path}/common",
      "${appspawn_path}/modules/ace_adapter",
      "${appspawn_path}/modules/common",
      "${appspawn_path}/modules/modulemgr",
      "${appspawn_path}/standard",
      "${appspawn_innerkits_path}/client",
      "${appspawn_innerkits_path}/include",
    ]

    # Test specific include dir
    include_dirs += [
      "${appspawn_path}/test/mock/appspawn_ace_mock",
      "${appspawn_path}/test/unittest",
    ]

    # Target code sources
    sources = [ "${appspawn_path}/modules/ace_adapter/ace_adapter.cpp" ]

    # Dependent code sources
    sources += [
      "${appspawn_path}/modules/ace_adapter/command_lexer.cpp",
      "${appspawn_path}/modules/modulemgr/appspawn_modulemgr.c",
      "${appspawn_path}/standard/appspawn_appmgr.c",
      "${appspawn_path}/standard/appspawn_msgmgr.c",
      "${appspawn_path}/util/src/appspawn_utils.c",
      "${appspawn_path}/util/src/appspawndf_utils.cpp",
    ]

    # Test specific sources
    sources += [
      "${appspawn_path}/test/mock/appspawn_ace_mock/appspawn_ace_mock_test.cpp",
      "app_spawn_ace_test.cpp",
    ]

    configs = [ "${appspawn_path}:appspawn_config" ]
    external_deps = [
      "ability_runtime:appkit_native",
      "ability_runtime:runtime",
      "cJSON:cjson",
      "c_utils:utils",
      "config_policy:configpolicy_util",
      "googletest:gmock_main",
      "hilog:libhilog",
      "hitrace:hitrace_meter",
      "init:libbegetutil",
      "napi:ace_napi",
      "resource_management:global_resmgr",
    ]
  }
}
