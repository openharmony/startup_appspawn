# Copyright (c) 2026 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/startup/appspawn/appspawn.gni")
import("//base/startup/appspawn/modules/sandbox/sandbox.gni")
import(
    "//base/startup/appspawn/test/unittest/app_spawn_standard_test/server_helper_test/server_helper_test.gni")
import("//build/test.gni")

if (!defined(ohos_lite)) {
  ohos_unittest("AppSpawn_Beget_Test") {
    module_out_path = "appspawn/appspawn"
    cflags = [ "-Dprivate=public" ]

    if (appspawn_unittest_coverage) {
      cflags += [ "--coverage" ]
      ldflags = [ "--coverage" ]
      cflags_cc = [ "--coverage" ]
    }

    deps = []
    defines = [
      "APPSPAWN_BASE_DIR=\"/data/appspawn_ut\"",
      "APPSPAWN_LABEL=\"APPSPAWN_UT\"",
      "APPSPAWN_TEST",
      "APPSPAWN_CLIENT",
      "APPSPAWN_DEBUG",
    ]

    # Target code include dir
    include_dirs = [
      "${appspawn_path}/common",
      "${appspawn_path}/modules/common",
      "${appspawn_path}/modules/modulemgr",
      "${appspawn_path}/modules/module_engine/include",
      "${appspawn_path}/standard",
      "${appspawn_innerkits_path}/client",
      "${appspawn_innerkits_path}/permission",
    ]
    include_dirs += appspawn_sandbox_inc

    # Test specific include dir
    include_dirs += [ "${appspawn_path}/test/unittest/app_spawn_standard_test/app_spawn_beget_test" ]
    include_dirs += server_helper_test_inc

    # Target code sources
    sources = [ "${appspawn_path}/modules/common/appspawn_begetctl.c" ]

    # Dependent code sources
    sources += [
      "${appspawn_innerkits_path}/client/appspawn_client.c",
      "${appspawn_innerkits_path}/client/appspawn_msg.c",
      "${appspawn_innerkits_path}/permission/appspawn_mount_permission.c",
      "${appspawn_path}/common/appspawn_server.c",
      "${appspawn_path}/common/appspawn_trace.cpp",
      "${appspawn_path}/modules/common/appspawn_dfx_dump.cpp",
      "${appspawn_path}/modules/modulemgr/appspawn_modulemgr.c",
      "${appspawn_path}/standard/appspawn_appmgr.c",
      "${appspawn_path}/standard/appspawn_msgmgr.c",
      "${appspawn_path}/standard/appspawn_service.c",
      "${appspawn_path}/util/src/appspawn_utils.c",
      "${appspawn_path}/util/src/appspawndf_utils.cpp",
    ]
    sources += appspawn_sandbox_src

    # Test specific sources
    sources += [
      "app_spawn_beget_test.cpp",
      "beget_test_helper.cpp",
    ]
    sources += server_helper_test_src

    configs = [ "${appspawn_path}:appspawn_config" ]
    external_deps = [
      "access_token:libtokenid_sdk",
      "cJSON:cjson",
      "c_utils:utils",
      "config_policy:configpolicy_util",
      "faultloggerd:libdfx_dumpcatcher",
      "ffrt:libffrt",
      "googletest:gmock_main",
      "hilog:libhilog",
      "hitrace:hitrace_meter",
      "init:libbegetutil",
    ]
    if (build_selinux) {
      defines += [ "WITH_SELINUX" ]
      external_deps += [ "selinux_adapter:libhap_restorecon" ]
    }
  }
}
